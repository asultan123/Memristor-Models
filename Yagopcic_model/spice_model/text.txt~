* SPICE model for memristive devices
* Created by Chris Yakopcic 
* Last Update: 8/9/2011
*
* Connections:
* TE - top electrode
* BE - bottom electrode
* XSV - External connection to plot state variable
* that is not used otherwise

simulator lang = spectre

* Fitting parameters to model different devices
* a1, a2, b:      Parameters for IV relationship
* Vp, Vn:         Pos. and neg. voltage thresholds
* Ap, An:         Multiplier for SV motion intensity
* xp, xn:         Points where SV motion is reduced
* alphap, alphan: Rate at which SV motion decays
* xo:             Initial value of SV
* eta:            SV direction relative to voltage

*The problematic parameters
*parameters a1=0.17 a2=0.17 b=0.05 Vp=0.16 Vn=0.15 Ap=4000 An=4000 xp=0.3 xn=0.5 alphap=1 alphan=5 xo=0.11 eta=1

parameters a1=.165 a2=.165 b=0.05 Vp=2.86 Vn=3.56 Ap=550000000 An=400000000 xp=0.9 xn=0.9 alphap=20 alphan=20 xo=0.01 eta=1

* Multiplicitive functions to ensure zero state
* variable motion at memristor boundaries
real wp(real V){
return (xp/(1-xp))-(V/(1-xp))+1;
} 

real wn(real V) {
return V/(1-xn);
}

* Function G(V(t)) - Describes the device threshold
real G(real V) {
return (V <= Vp)?((V >= -Vn)?0:-An*(exp(-V)-exp(Vn))):Ap*(exp(V)-exp(Vp));
}

* Function F(V(t),x(t)) - Describes the SV motion 
real F(real V1,real V2) {
return (eta*V1>=0)? ((V2>= xp)?exp(-alphap*(V2-xp))*wp(V2):1) : ((V2<=(1-xn))?exp(alphan*(V2+xn-1))*wn(V2):1);
}

* IV Response - Hyperbolic sine due to MIM structure
real IVRel(real V1,real V2) {
return (V1>=0)?a1*V2*sinh(b*V1):a2*V2*sinh(b*V1);
}

subckt MEM_YAKOPCIC (TE BE XSV) 

ic XSV = xo
ic TE = 0
ic BE = 0 

* Circuit to determine state variable
* dx/dt = F(V(t),x(t))*G(V(t))
CX (XSV 0) capacitor c=1

GX (0 XSV) bsource i=eta*F(V(TE,BE),V(XSV,0))*G(V(TE,BE))
* Current source for memristor IV response
GM (TE BE) bsource i=IVRel(V(TE,BE),V(XSV,0))

ends MEM_YAKOPCIC
